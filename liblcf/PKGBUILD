# Maintainer: carstene1ns https://aur.archlinux.org/packages/liblcf

pkgname=liblcf
pkgver=0.4.1
pkgrel=2
pkgdesc="Library to handle RPG Maker 2000/2003 and EasyRPG projects"
arch=('i686' 'x86_64')
url="https://easy-rpg.org/"
license=('MIT')
depends=('gcc-libs' 'expat' 'icu')
source=("https://easy-rpg.org/downloads/player/liblcf-$pkgver.tar.gz")
sha256sums=('038a3c382759a27956338d983043f330afeb71caefc4f5de8966925ebff539fe')

build () {
  cd liblcf-$pkgver

  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

check() {
  if [ "$CARCH" == "x86_64" ]; then
    make -C liblcf-$pkgver check
  else
    true
  fi
}

package () {
  cd liblcf-$pkgver

  make DESTDIR="$pkgdir/" install
  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
